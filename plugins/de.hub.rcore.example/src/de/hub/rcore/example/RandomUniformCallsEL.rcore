package de.hub.rcore.example

import de.hub.randomemf.runtime.Ocl

import static de.hub.randomemf.runtime.Random.*
import org.eclipse.emf.ecore.EClass

generator RandomUniformCallsEL for el in "platform:/resource/de.hub.rcore.example/model/example-language.ecore" {	
	
	root: ELModel ->
		packages += ELPredefinitions.pkg(self)
		packages += Package#1
	;
	
	Package: ELPackage ->
		name := ClassName(Normal(3,2)).toLowerCase	
		classes += Class#10
	;
	
	Class: ELClass ->
		name := ClassName(Normal(4,2))
		fields += Field#(Content()*0.4) as int
		methods += Constructor(self.name)#(Content*0.5) as int
		methods += Method#(Content()*3.5) as int
		innerClasses += Class#(if (depth < 4 && UniformBool(0.3)) 1 else 0)  
	;
	
	Constructor(String name): ELMethod ->
		name := name	
		parameters += Variable#(Content()*0.2) as int
	;
	
	Field: ELField ->
		name := VarName(Normal(4,2))
		array := UniformBool(0.1)		
		type:ELClass := @(Uniform(Ocl.collectAll(model.packages)[it.classes]))
	;
	
	Variable: ELVariable ->
		name := VarName(Normal(2,1))
		array := UniformBool(0.1)		
		type:ELClass := @(Uniform(Ocl.collectAll(model.packages)[it.classes]))
	;
	
	Method:ELMethod ->
		name := MethodName(Normal(5,3))
		type := @(if (UniformBool(0.3)) null else Uniform(Ocl.collectAll(model.packages)[it.classes]))	
		parameters += Variable#(Content()*0.3) as int
		block := Block
	;
	
	Block:ELBlock ->
		statements += Statement#(Math.round(Content()*0.7) as int)+1
	;
	
	alter Statement:ELStatement ->
		BlockStatement#6 | MethodCall#depth*2 | Assignment#depth*4
	;
	
	BlockStatement: ELBlockStatement ->
		kind := Uniform(model.blocks)
		block += Block#self.kind.blocks
		expressions += Expression#self.kind.expressions
	;
	
	alter Expression: ELExpression ->
		MethodCall#2 | OpCall#4 | Literal#((depth*1.2) as int)
	;
	
	alter Literal: ELLiteral ->
		StringLiteral | IntLiteral | BoolLiteral
	;
	
	StringLiteral: ELLiteral -> value := LoremIpsum((Content()*0.2) as int);
	IntLiteral: ELLiteral -> value := "" + Normal(10,5);
	BoolLiteral: ELLiteral -> value := "" + UniformBool(0.5);
	
	OpCall: ELOpCall ->
		kind := Uniform(model.ops)
		arguments += Expression#self.kind.operands
	;
	
	MethodCall: ELCall ->
		callee := @(Uniform(RandomELUtil.uniformMethods(model, self)))
		arguments += Expression#(Content()*0.3) as int
	;
	
	Access: ELAccess ->
		variable := @(Uniform(Ocl.collectAll(model.packages)[Ocl.collectAll(it.classes)[it.fields]]))
	;
	
	Assignment: ELAssignment ->
		assignee := @(Uniform(Ocl.collectAll(model.packages)[Ocl.collectAll(it.classes)[it.fields]]))
		expr := Expression()#1
	;
}