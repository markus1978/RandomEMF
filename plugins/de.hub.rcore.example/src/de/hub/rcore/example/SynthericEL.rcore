package de.hub.rcore.example

import de.hub.randomemf.runtime.Ocl

import static de.hub.randomemf.runtime.Random.*
import cern.jet.random.Uniform
import de.hub.rcore.example.el.ELPackage

generator RandomEL for el in "platform:/resource/de.hub.rcore.example/model/example-language.ecore" {	
	
	root: ELModel ->
		packages += ELPredefinitions.pkg(self)
		packages += Package#3
	;
	
	Package: ELPackage ->
		name := ClassName(Normal(3,2)).toLowerCase	
		classes += Class#10
	;
	
	Class: ELClass ->
		name := ClassName(Normal(4,2))
		fields += Field#3
		methods += Constructor#1
		methods += Method#8
		innerClasses += Class#(if (depth < 4 && self.innerClasses.size % 3 == 0) 1 else 0)  
	;
	
	Constructor: ELMethod ->
		name := "new"	
		parameters += Variable#1 as int
	;
	
	Field: ELField ->
		name := VarName(Normal(4,2))
		array := false		
		type:ELClass := Uniform(Ocl.collectAll(model.packages)[it.classes])
	;
	
	Variable: ELVariable ->
		name := VarName(Normal(2,1))
		array := false		
		type:ELClass := Uniform(Ocl.collectAll(model.packages)[it.classes])
	;
	
	Method:ELMethod ->
		name := MethodName(Normal(5,3))
		type := if (UniformBool(0.3)) null else Uniform(Ocl.collectAll(model.packages)[it.classes])	
		parameters += Variable#2
		block := Block
	;
	
	Block:ELBlock ->
		statements += Statement#4
	;
	
	alter Statement:ELStatement ->
		BlockStatement#if (depth < 8) 1 else 0 | MethodCall#3 | Assignment#depth*4 // continue here TDB TODO
	;
	
	BlockStatement: ELBlockStatement ->
		kind := Uniform(model.blocks)
		block += Block#self.kind.blocks
		expressions += Expression#self.kind.expressions
	;
	
	alter Expression: ELExpression ->
		MethodCall#6 | OpCall#4 | Literal#(depth*1.2) as int
	;
	
	alter Literal: ELLiteral ->
		StringLiteral | IntLiteral | BoolLiteral
	;
	
	StringLiteral: ELLiteral -> value := LoremIpsum(Content());
	IntLiteral: ELLiteral -> value := "" + Normal(10,5);
	BoolLiteral: ELLiteral -> value := "" + UniformBool(0.5);
	
	OpCall: ELOpCall ->
		kind := Uniform(model.ops)
		arguments += Expression#self.kind.operands
	;
	
	MethodCall: ELCall ->
		callee := @(Uniform(Ocl.collectAll(model.packages)[Ocl.collectAll(it.classes)[it.methods]]))
		arguments += Expression#(Content()*0.3) as int
	;
	
	Access: ELAccess ->
		variable := @(Uniform(Ocl.collectAll(model.packages)[Ocl.collectAll(it.classes)[it.fields]]))
	;
	
	Assignment: ELAssignment ->
		assignee := @(Uniform(Ocl.collectAll(model.packages)[Ocl.collectAll(it.classes)[it.fields]]))
		expr := Expression()#1
	;
}